# Maintainer: Marco Cantoro <marco dot cantoro92 at outlook dot it>

pkgname=xmenu-bull-git
pkgver=4.5.5.r153.20a96bd
pkgrel=1
pkgdesc="A X11 menu utility"
arch=('x86_64')
url="https://github.com/phillbush/xmenu"
license=('MIT')
depends=(
  'expat'
  'fontconfig'
  'freetype2'
  'glib2'
  'graphite'
  'harfbuzz'
  'imlib2'
  'libpng'
  'libx11'
  'libxau'
  'libxcb'
  'libxdmcp'
  'libxext'
  'libxft'
  'libxinerama'
  'libxrender'
  'pcre'
  'zlib'
)
makedepends=('git')
provides=("${pkgname%-bull-git}")
conflicts=("${pkgname%-bull-git}")
source=('git+https://github.com/CantoroMC/xmenu')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-bull-git}"
	printf "4.5.5.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-bull-git}"
	make
}

package() {
  local licdir="$pkgdir/usr/share/licenses/${pkgname%-bull-git}"
  local docdir="$pkgdir/usr/share/doc/${pkgname%-bull-git}"

	cd "$srcdir/${pkgname%-bull-git}"
	make PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm0644 --target-directory "$licdir" "$srcdir/${pkgname%-bull-git}/LICENSE"
  install -Dm0644 --target-directory "$docdir" "$srcdir/${pkgname%-bull-git}/README.md"
  install -Dm0644 --target-directory "$docdir" "$srcdir/${pkgname%-bull-git}/CHANGELOG"
}
