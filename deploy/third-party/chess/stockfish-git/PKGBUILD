# Maintainer: Marco Cantoro <marco dot cantoro92 at outlook dot it>

_pkgname=Stockfish
pkgname=stockfish-bull-git
pkgver=13.r5207.594e2ac9
pkgrel=1
pkgdesc="Free, powerful UCI chess engine derived from Glaurung 2.1."
arch=('i686' 'x86_64')
url="http://stockfishchess.org/"
license=('GPL3')
makedepends=('git')
optdepends=(
  'xboard: Graphical user interfaces for chess'
  'polyglot: UCI/USI/UCCI to XBoard adapter'
)
provides=("${pkgname%-bull-git}")
conflicts=("${pkgname%-bull-git}")
source=('git+https://github.com/official-stockfish/Stockfish.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "13.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname/src"
  if [[ "$CARCH" == "i686" ]]; then
    _arch=x86-32
  elif grep -q popcnt /proc/cpuinfo; then
    _arch=x86-64-sse3-popcnt
  else
    _arch=x86-64-modern
  fi
  make -j$(nproc) profile-build ARCH=$_arch COMP=gcc COMPCXX=g++
}

package() {
  cd "$srcdir/$_pkgname/src"
  make PREFIX="$pkgdir/usr" install

  local docdir="$pkgdir/usr/share/doc/${pkgname%-bull-git}"
  install -Dm0644 --target-directory "$docdir" "$srcdir/$_pkgname/README.md"
}
