# Maintainer: Marco Cantoro <marco dot cantoro92 at outlook dot it>

pkgname=dwm-bull-git
pkgver=6.2.r1752.a416242
pkgrel=1
pkgdesc="Dynamic window manager for X"
arch=('x86_64')
url="https://dwm.suckless.org"
license=('MIT')
depends=(
  'dmenu'
  'expat'
  'fontconfig'
  'freetype2'
  'glib2'
  'graphite'
  'harfbuzz'
  'libpng'
  'libx11'
  'libxau'
  'libxcb'
  'libxdmcp'
  'libxext'
  'libxft'
  'libxinerama'
  'libxrender'
  'pcre'
  'st'
  'zlib'
)
makedepends=('git')
provides=("${pkgname%-bull-git}")
conflicts=("${pkgname%-bull-git}")
source=('git+https://github.com/CantoroMC/dwm')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-bull-git}"
  printf "6.2.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/${pkgname%-bull-git}"
  make
}

package() {
  local licdir="$pkgdir/usr/share/licenses/${pkgname%-bull-git}"
  local docdir="$pkgdir/usr/share/doc/${pkgname%-bull-git}"

  cd "$srcdir/${pkgname%-bull-git}"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm0644 --target-directory "$licdir" "$srcdir/${pkgname%-bull-git}/LICENSE"
  install -Dm0644 --target-directory "$docdir" "$srcdir/${pkgname%-bull-git}/README.org"
}
