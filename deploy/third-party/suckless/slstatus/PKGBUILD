# Maintainer: Marco Cantoro <marco dot cantoro92 at outlook dot it>

pkgname=slstatus-bull-git
pkgver=1.0.r5.4e1e424
pkgrel=1
pkgdesc="Status monitor for window managers that use WM_NAME or stdin to fill the status bar"
arch=('x86_64')
url="https://tools.suckless.org/slstatus"
license=('custom:ISC')
depends=(
  'libxau'
  'libxcb'
  'libxdmcp'
)
makedepends=('git')
provides=("${pkgname%-bull-git}")
conflicts=("${pkgname%-bull-git}")
source=('git+https://github.com/CantoroMC/slstatus')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-bull-git}"
  printf "1.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/${pkgname%-bull-git}"
  make
}

package() {
  local licdir="$pkgdir/usr/share/licenses/${pkgname%-bull-git}"
  local docdir="$pkgdir/usr/share/doc/${pkgname%-bull-git}"

  cd "$srcdir/${pkgname%-bull-git}"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm0644 --target-directory "$licdir" "$srcdir/${pkgname%-bull-git}/LICENSE"
  install -Dm0644 --target-directory "$docdir" "$srcdir/${pkgname%-bull-git}/README"
}
