# Maintainer: Marco Cantoro <marco dot cantoro92 at outlook dot it>

pkgname=dmenu-bull-git
pkgver=5.0.r1.cd3effe
pkgrel=1
pkgdesc="A dynamic menu for X"
arch=('x86_64')
url="https://tools.suckless.org/dmenu"
license=('MIT')
depends=(
  'expat'
  'fontconfig'
  'freetype2'
  'glib2'
  'graphite'
  'harfbuzz'
  'libpng'
  'libxau'
  'libxcb'
  'libxdmcp'
  'libx11'
  'libxft'
  'libxrender'
  'pcre'
  'zlib'
)
makedepends=('git')
provides=("${pkgname%-bull-git}" "stest")
conflicts=("${pkgname%-bull-git}" "stest")
source=('git+https://github.com/CantoroMC/dmenu')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-bull-git}"
	printf "5.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-bull-git}"
	make
}

package() {
  local licdir="$pkgdir/usr/share/licenses/${pkgname%-bull-git}"
  local docdir="$pkgdir/usr/share/doc/${pkgname%-bull-git}"

	cd "$srcdir/${pkgname%-bull-git}"
	make PREFIX=/usr DESTDIR="$pkgdir" install

  install --mode 0644 -D --target-directory "$licdir" "$srcdir/${pkgname%-bull-git}/LICENSE"
  install --mode 0644 -D --target-directory "$docdir" "$srcdir/${pkgname%-bull-git}/README.md"
}
