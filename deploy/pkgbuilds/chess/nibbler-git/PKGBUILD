# Maintainer: Valerii Huz <ghotrix at gmail dot com>

_pkgname=nibbler
pkgname=nibbler-bull-git
pkgver=2.1.6
pkgrel=1
pkgdesc="Leela Chess Zero (Lc0) interface"
arch=('i686' 'x86_64')
url="https://github.com/rooklift/nibbler"
license=('GPLv3')
depends=('electron')
makedepends=('git')
provides=("${pkgname%-bull-git}")
conflicts=("${pkgname%-bull-git}")
source=("${_pkgname}::git+${url}.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  printf "2.1.6.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"
  printf "#!/usr/bin/env bash \nelectron /opt/${_pkgname}" > nibbler
}

package() {
  cd "${srcdir}/${_pkgname}"

  install -m755 -d "${pkgdir}/opt/${_pkgname}/modules"
  install -m755 -d "${pkgdir}/opt/${_pkgname}/pieces"
  install -m755 -d "${pkgdir}/opt/${_pkgname}/misc"
  cp -r "$srcdir/${_pkgname}"/src/modules/* "${pkgdir}/opt/${_pkgname}/modules"
  cp -r "$srcdir/${_pkgname}"/src/pieces/* "${pkgdir}/opt/${_pkgname}/pieces"
  cp -r "$srcdir/${_pkgname}"/src/misc/* "${pkgdir}/opt/${_pkgname}/misc"
  cp -r "$srcdir/${_pkgname}"/src/*.{css,html,js,json} "${pkgdir}/opt/${_pkgname}"

  install -Dm755 nibbler $pkgdir/usr/bin/nibbler

  local licdir="$pkgdir/usr/share/licenses/${pkgname%-bull-git}"
  local docdir="$pkgdir/usr/share/doc/${pkgname%-bull-git}"
  install --mode 0644 -D --target-directory "$licdir" "$srcdir/${pkgname%-bull-git}/LICENSE.md"
  install --mode 0644 -D --target-directory "$docdir" "$srcdir/${pkgname%-bull-git}/README.md"
}
