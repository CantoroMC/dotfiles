#compdef xsel

local input='(-a --append -f --follow -i --input)'
local operation='(-c --clear -d --delete -k --keep -x --exchange)' 
local selection='(-p --primary -s --secondary -b --clipboard)'

_arguments -s \
  $input{-a,--append}'[append standard input to the selection]' \
  $input{-f,--follow}'[append to selection as standard input grows]' \
  $input{-i,--input}'[read standard input into the selection]' \
  {-o,--output}'[write the selection to standard output]' \
  $operation{-c,--clear}'[clear the selection]' \
  $operation{-d,--delete}'[request that the current selection be deleted]' \
  $selection{-p,--primary}'[operate on the PRIMARY selection (default)]' \
  $selection{-s,--secondary}'[operate on the SECONDARY selection]' \
  $selection{-b,--clipboard}'[operate on the CLIPBOARD selection]' \
  $operation{-k,--keep}'[do not modify the selections, but make the PRIMARY and SECONDARY selections persist even after the programs they were selected in exit]' \
  $operation{-x,--exchange}'[exchange the PRIMARY and SECONDARY selections]' \
  '--display[specify the server to use; see X(1)]:X display:_x_display' \
  {-t,--selectionTimeout}'[specify the timeout in milliseconds within which the selection must be retrieved]:number' \
  {-l,--logfile}'[specify the file to log errors to when detached (default $HOME/.xsel.log)]:log file:_files' \
  {-n,--nodetach}'[do not detach from the controlling terminal]' \
  {-h,--help}'[display usage information and exit]' \
  {-v,--verbose}'[print informative messages; additional instances of -v raise the debugging level]' \
  '--version[output version information and exit]'
