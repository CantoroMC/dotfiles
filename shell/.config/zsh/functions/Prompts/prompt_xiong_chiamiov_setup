prompt_xiong_chiamiov_help () {
  cat <<EOH
This prompt is color-scheme-able.  You can invoke it thus:

  prompt xiong_chiamiov [<bull-bar-and-cwd> [<color2> [<color3>]]]

where the parameters are the colors for the theme-bar and current
working directory, user@host text, and color3 text respectively.  The
default colors are blue, white, and green.  This theme works best
with a dark background.

Recommended fonts for this theme: NerdFont.
If you don't have any of these, the 8-bit characters will probably look
stupid.
EOH
}

prompt_xiong_chiamiov_setup () {
  local color1=${1:-'blue'}
  local color2=${2:-'white'}
  local color3=${3:-'green'}

  local user_at_machine="%B%F{$color1}┌─[%f%F{$color3}%n%f%F{$color1}@%f%b%F{$color2}%M%f%B%F{$color1}]%f%b"
  local sep=" - "
  local dir="%B%F{$color1}[%f%F{$color2}%(7~|%-3~/.../%3~|%~)%f%F{$color1}]%b"
  local start="%B%F{$color1}└─[%f%(?.%F{$color3}$.%F{red}$)%f%F{$color1}]%b"

  local nr_jobs="%B%F{$color3}%(1j.jobs: %j.)%f%b"
  local hist_len="%B%F{$color1}[%f%F{$color2}%h%f%F{$color1}]%b"
  local return_code="%(?..%F{red}%? %{↵ %2G%})%f"


  # Info
  ZSH_THEME_GIT_PROMPT_PREFIX="%F{$color2}:%B"
  ZSH_THEME_GIT_PROMPT_SUFFIX="%f%b"
  ZSH_THEME_GIT_PROMPT_DIRTY="%{✗  %3G%}%b%F{$color3}%})"
  ZSH_THEME_GIT_PROMPT_CLEAN="%{✓  %3G%}%b%F{$color3})"
  # Status
  ZSH_THEME_GIT_PROMPT_ADDED="%B%F{$color2} A %f%b"
  ZSH_THEME_GIT_PROMPT_MODIFIED="%B%F{$color2} M%f%b"
  ZSH_THEME_GIT_PROMPT_DELETED="%B%F{$color1} D%f%b"
  ZSH_THEME_GIT_PROMPT_RENAMED="%B%F{$color2} R%f%b"
  ZSH_THEME_GIT_PROMPT_UNMERGED="%B%F{$color1} Um%f%b"
  ZSH_THEME_GIT_PROMPT_UNTRACKED="%B%F{$color1} Ut%f%b"
  # SHA
  ZSH_THEME_GIT_PROMPT_SHA_BEFORE="%F{$color3}(%B%F{$color1}"
  ZSH_THEME_GIT_PROMPT_SHA_AFTER="%f%b"
  # Remote
  ZSH_THEME_GIT_COMMITS_AHEAD_PREFIX="%B%F{$color3}%{⇡%1G%}:"
  ZSH_THEME_GIT_COMMITS_AHEAD_SUFFIX="%f%b"
  ZSH_THEME_GIT_COMMITS_BEHIND_PREFIX="%B%F{$color3}%{⇣%1G%}:"
  ZSH_THEME_GIT_COMMITS_BEHIND_SUFFIX="%f%b"

  local git_branch='$(git_prompt_short_sha)$(git_prompt_info)'
  local git_status='$(git_prompt_status)'
  local git_commits='$(git_commits_ahead) $(git_commits_behind)'


  PS1="${user_at_machine}${sep}${dir}${sep}${hist_len}$prompt_newline${start}${sep}${MODE_INDICATOR_PROMPT} ${return_code} "
  PS2="%B%F{$color1}%_>%f%b"
  RPS1="${git_commits}${git_branch}${git_status}"

  prompt_opts=(cr subst percent)
}

prompt_xiong_chiamiov_preview () {
  local color colors
  colors=(red yellow green blue magenta)

  if (( ! $#* )); then
    for (( i = 1; i <= $#colors; i++ )); do
      color=$colors[$i]
      prompt_preview_theme xiong_chiamiov $color
      print
    done
    prompt_preview_theme xiong_chiamiov white grey blue
  else
    prompt_preview_theme xiong_chiamiov "$@"
  fi
}

prompt_xiong_chiamiov_setup "$@"

# vim:ft=zsh
