prompt_bull_help () {
  cat <<EOH
This prompt is color-scheme-able.  You can invoke it thus:

  prompt bull [<color1> [<color2> [<color3>]]]

where the parameters are the colors for the theme-bar and current
working directory, user@host text, and color3 text respectively.  The
default colors are green, white, and blue.  This theme works best
with a dark background.

Recommended fonts for this theme: NerdFont.
If you don't have any of these, the 8-bit characters will probably look
stupid.
EOH
}

prompt_bull_setup () {
  local -A schars
  autoload -Uz prompt_special_chars
  prompt_special_chars

  local color1=${1:-'green'}
  local color2=${2:-'white'}
  local color3=${3:-'blue'}

  local intro="%B%F{#138ac1}%{ %2G%}%f%b"
  local dir_bar="%B%F{$color1}%K{$color1}$schars[333]$schars[262]$schars[261]$schars[260]%F{$color2}%K{$color1}%n at %M in %(7~|%-3~/.../%3~|%~)%b%F{$color1}%k$schars[333]$schars[262]$schars[261]$schars[260]%f"
  local nr_jobs="%B%F{$color3}%(1j.jobs: %j.)%f%b"
  local hist_len="%B%F{$color2}%h%f%b"

  local return_code="%(?..%F{red}%? %{↵ %2G%})%f"
  local start="%(?.%F{$color1}%{➜ %2G%}.%F{red}%{➜ %2G%})%f"

  # Info
  ZSH_THEME_GIT_PROMPT_PREFIX="%F{$color2}:%B"
  ZSH_THEME_GIT_PROMPT_SUFFIX="%f%b"
  ZSH_THEME_GIT_PROMPT_DIRTY="%{✗  %3G%}%b%F{$color3}%})"
  ZSH_THEME_GIT_PROMPT_CLEAN="%{✓  %3G%}%b%F{$color3})"
  # Status
  ZSH_THEME_GIT_PROMPT_ADDED="%B%F{$color2} A %f%b"
  ZSH_THEME_GIT_PROMPT_MODIFIED="%B%F{$color2} M%f%b"
  ZSH_THEME_GIT_PROMPT_DELETED="%B%F{$color1} D%f%b"
  ZSH_THEME_GIT_PROMPT_RENAMED="%B%F{$color2} R%f%b"
  ZSH_THEME_GIT_PROMPT_UNMERGED="%B%F{$color1} Um%f%b"
  ZSH_THEME_GIT_PROMPT_UNTRACKED="%B%F{$color1} Ut%f%b"
  # SHA
  ZSH_THEME_GIT_PROMPT_SHA_BEFORE="%F{$color3}(%B%F{$color1}"
  ZSH_THEME_GIT_PROMPT_SHA_AFTER="%f%b"
  # Remote
  ZSH_THEME_GIT_COMMITS_AHEAD_PREFIX="%B%F{$color3}%{⇡%1G%}:"
  ZSH_THEME_GIT_COMMITS_AHEAD_SUFFIX="%f%b"
  ZSH_THEME_GIT_COMMITS_BEHIND_PREFIX="%B%F{$color3}%{⇣%1G%}:"
  ZSH_THEME_GIT_COMMITS_BEHIND_SUFFIX="%f%b"

  local git_branch='$(git_prompt_short_sha)$(git_prompt_info)'
  local git_status='$(git_prompt_status)'
  local git_commits='$(git_commits_ahead) $(git_commits_behind)'

  PS1=" ${intro} ${dir_bar} ${nr_jobs} ${hist_len} $prompt_newline ${MODE_INDICATOR_PROMPT} ${return_code} ${start} "
  PS2="%F{$color1}%K{$color1}$schars[333]$schars[262]$schars[261]$schars[260]%f%k%_>"
  RPS1="${git_commits}${git_branch}${git_status}"
  SPROMPT="zsh: correct %F{red}'%R'%f to %F{green}'%r'%f [%B%Un%u%bo, %B%Uy%u%bes, %B%Ue%u%bdit, %B%Ua%u%bbort]? "


  prompt_opts=(cr subst percent)
}

prompt_bull_preview () {
  local color colors
  colors=(red yellow green blue magenta)

  if (( ! $#* )); then
    for (( i = 1; i <= $#colors; i++ )); do
      color=$colors[$i]
      prompt_preview_theme bull $color
      print
    done
    prompt_preview_theme bull white grey blue
  else
    prompt_preview_theme bull "$@"
  fi
}

prompt_bull_setup "$@"

# vim:ft=zsh
